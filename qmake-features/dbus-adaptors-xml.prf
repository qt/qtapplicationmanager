
load(moc)
qtPrepareTool(QDBUSXML2CPP, qdbusxml2cpp)

adaptors_header.commands = $$QDBUSXML2CPP -i dbus-utilities.h -a ${QMAKE_FILE_OUT}: ${QMAKE_FILE_IN}
adaptors_header.depends += $$QDBUSXML2CPP_EXE
adaptors_header.output = ${QMAKE_FILE_BASE}_adaptor$${first(QMAKE_EXT_H)}
adaptors_header.name = DBUS adaptor header for ${QMAKE_FILE_IN}
adaptors_header.variable_out = dbus_adaptor_headers
adaptors_header.input = ADAPTORS_XML
adaptors_header.CONFIG += target_predep

QMAKE_EXTRA_COMPILERS += adaptors_header

adaptors_moc.commands = $$moc_header.commands
adaptors_moc.depends += $$QMAKE_MOC_EXE
adaptors_moc.output = $$moc_header.output
adaptors_moc.input = dbus_adaptor_headers
adaptors_moc.variable_out = GENERATED_SOURCES
adaptors_moc.name = DBUS adaptor MOC headers

QMAKE_EXTRA_COMPILERS += adaptors_moc
